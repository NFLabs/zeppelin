/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
description = 'A web-based notebook that enables interactive data analytics.'
apply plugin: 'base'

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath "io.spring.gradle:dependency-management-plugin:0.2.1.RELEASE"
    classpath "net.saliman:gradle-cobertura-plugin:2.2.5"
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.2.1'
}

// Global setup for the subprojects
subprojects {
  apply plugin: 'eclipse'
  apply plugin: 'maven'
  apply plugin: 'java'
  apply plugin: "io.spring.dependency-management"

  group 'com.nflabs.zeppelin'

  // Force to use Java 1.7
  sourceCompatibility = JavaVersion.VERSION_1_7

  repositories {
    mavenLocal()
    mavenCentral()
  }

  apply from: "$rootProject.projectDir/gradle/dependencyManagement.gradle"
  apply from: "$rootProject.projectDir/gradle/codeQuality.gradle"

  dependencies {
    testCompile group: "junit", name: "junit", version: "4.11"
    testCompile "org.mockito:mockito-core:1.9.5"
  }
}

clean {
  doLast {
    def interpreters = new File(rootDir, 'interpreter')
    interpreters.deleteDir()
    
    def distribution = new File(rootDir, 'dist')
    distribution.deleteDir()
    
    def target = new File(rootDir, 'lib')
    target.deleteDir()
  }
  delete file("zeppelin-web/src")
}

